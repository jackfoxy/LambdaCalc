(* 
   The lexical analyzer: lexer.ml is generated automatically
   from lexer.mll.
   
   The only modification commonly needed here is adding new keywords to the 
   list of reserved words at the top.  
*)

{
(*
Copyright (c) 2003, Benjamin C. Pierce
Copyright (c) 2012, Jack Pappas
All rights reserved.

This code is provided under the terms of the 2-clause ("Simplified") BSD license.
See LICENSE.TXT for licensing details.
*)

module Lexer

open System
open Support.Error
open Parser
open Microsoft.FSharp.Text.Lexing

let lexeme lexbuf =
    LexBuffer<char>.LexemeString lexbuf

let reservedWords = [
  (* Keywords *)
  ("if", fun i -> Parser.IF i);
  ("then", fun i -> Parser.THEN i);
  ("else", fun i -> Parser.ELSE i);
  ("true", fun i -> Parser.TRUE i);
  ("false", fun i -> Parser.FALSE i);
  ("lambda", fun i -> Parser.LAMBDA i);
  ("timesfloat", fun i -> Parser.TIMESFLOAT i);
  ("succ", fun i -> Parser.SUCC i);
  ("pred", fun i -> Parser.PRED i);
  ("iszero", fun i -> Parser.ISZERO i);
  ("let", fun i -> Parser.LET i);
  ("in", fun i -> Parser.IN i);
  
  (* Symbols *)
  ("_", fun i -> Parser.USCORE i);
  ("'", fun i -> Parser.APOSTROPHE i);
  ("\"", fun i -> Parser.DQUOTE i);
  ("!", fun i -> Parser.BANG i);
  ("#", fun i -> Parser.HASH i);
  ("$", fun i -> Parser.TRIANGLE i);
  ("*", fun i -> Parser.STAR i);
  ("|", fun i -> Parser.VBAR i);
  (".", fun i -> Parser.DOT i);
  (";", fun i -> Parser.SEMI i);
  (",", fun i -> Parser.COMMA i);
  ("/", fun i -> Parser.SLASH i);
  (":", fun i -> Parser.COLON i);
  ("::", fun i -> Parser.COLONCOLON i);
  ("=", fun i -> Parser.EQ i);
  ("==", fun i -> Parser.EQEQ i);
  ("[", fun i -> Parser.LSQUARE i); 
  ("<", fun i -> Parser.LT i);
  ("{", fun i -> Parser.LCURLY i); 
  ("(", fun i -> Parser.LPAREN i); 
  ("<-", fun i -> Parser.LEFTARROW i); 
  ("{|", fun i -> Parser.LCURLYBAR i); 
  ("[|", fun i -> Parser.LSQUAREBAR i); 
  ("}", fun i -> Parser.RCURLY i);
  (")", fun i -> Parser.RPAREN i);
  ("]", fun i -> Parser.RSQUARE i);
  (">", fun i -> Parser.GT i);
  ("|}", fun i -> Parser.BARRCURLY i);
  ("|>", fun i -> Parser.BARGT i);
  ("|]", fun i -> Parser.BARRSQUARE i);

  (* Special compound symbols: *)
  (":=", fun i -> Parser.COLONEQ i);
  ("->", fun i -> Parser.ARROW i);
  ("=>", fun i -> Parser.DARROW i);
  ("==>", fun i -> Parser.DDARROW i);
]

(* Support functions *)

type buildfun = Info -> Parser.token
let symbolTable = System.Collections.Generic.Dictionary<string, buildfun> (1024)
do
  reservedWords
  |> List.iter symbolTable.Add

let createID i (str : string) =
    match symbolTable.TryGetValue str with
    | true, f -> f i
    | false, _ ->
        let c = str.[0]
        if c >= 'A' && c <= 'Z' then
            Parser.UCID { i = i; v = str; }
        else
            Parser.LCID { i = i; v = str; }

let rec lineno = ref 1
and depth = ref 0
and start = ref 0

and filename = ref ""
and startLex = ref dummyinfo

let newline (lexbuf : LexBuffer<char>) =
    incr lineno
    start := lexbuf.StartPos.pos_cnum   // Lexing.lexeme_start lexbuf

let info (lexbuf : LexBuffer<char>) =
    let lexeme_start = lexbuf.StartPos.pos_cnum  // Lexing.lexeme_start lexbuf
    createInfo !filename !lineno (lexeme_start - !start)

let stringBuffer = System.Text.StringBuilder (2048)

let resetStr () : unit =
    stringBuffer.Clear () |> ignore

let addStr (ch : char) =
    stringBuffer.Append ch |> ignore

let getStr () =
    stringBuffer.ToString ()

let extractLineno (yytext : string) offset =
    Int32.Parse (yytext.Substring(offset, (String.length yytext - offset)))
}


(* The main body of the lexical analyzer *)

rule main = parse
| [' ' '\009' '\012']+     { main lexbuf }

| [' ' '\009' '\012']*("\r")?"\n" { newline lexbuf; main lexbuf }

| "*/" { error (info lexbuf) "Unmatched end of comment" }

| "/*" { depth := 1; startLex := info lexbuf; comment lexbuf; main lexbuf }

| "# " ['0'-'9']+
    { lineno := extractLineno (lexeme lexbuf) 2 - 1; getFile lexbuf }

| "# line " ['0'-'9']+
    { lineno := extractLineno (lexeme lexbuf) 7 - 1; getFile lexbuf }

| ['0'-'9']+
    { Parser.INTV { i = info lexbuf; v = int (lexeme lexbuf); } }

| ['0'-'9']+ '.' ['0'-'9']+
    { Parser.FLOATV { i = info lexbuf; v= float (lexeme lexbuf); } }

| ['A'-'Z' 'a'-'z' '_']
  ['A'-'Z' 'a'-'z' '_' '0'-'9' '\'']*
    { createID (info lexbuf) (lexeme lexbuf) }

| ":=" | "<:" | "<-" | "->" | "=>" | "==>"
| "{|" | "|}" | "<|" | "|>" | "[|" | "|]" | "=="
    { createID (info lexbuf) (lexeme lexbuf) }

| ['~' '%' '\\' '+' '-' '&' '|' ':' '@' '`' '$']+
    { createID (info lexbuf) (lexeme lexbuf) }

| ['*' '#' '/' '!' '?' '^' '(' ')' '{' '}' '[' ']' '<' '>' '.' ';' '_' ','
   '=' '\'']
    { createID (info lexbuf) (lexeme lexbuf) }

| "\"" { resetStr(); startLex := info lexbuf; string lexbuf }

| eof { Parser.EOF (info lexbuf) }

| _  { error (info lexbuf) "Illegal character" }

and comment = parse
| "/*"
    { depth := !depth + 1; comment lexbuf }
| "*/"
    { depth := !depth - 1; if !depth > 0 then comment lexbuf }
| eof
    { error (!startLex) "Comment not terminated" }
| [^ '\n']
    { comment lexbuf }
| "\n"
    { newline lexbuf; comment lexbuf }

and getFile = parse
  " "* "\"" { getName lexbuf }

and getName = parse
  [^ '"' '\n']+ { filename := (lexeme lexbuf); finishName lexbuf }

and finishName = parse
  '"' [^ '\n']* { main lexbuf }

and string = parse
| '"'  { Parser.STRINGV {i = !startLex; v = getStr (); } }
| '\\' { addStr (escaped lexbuf); string lexbuf }
| '\n' { addStr '\n'; newline lexbuf; string lexbuf }
| eof  { error !startLex "String not terminated" }
| _    { addStr (lexbuf.LexemeChar 0); string lexbuf }

and escaped = parse
| 'n'     { '\n' }
| 't'     { '\t' }
| '\\'     { '\\' }
| '"'    { '\034'  }
| '\''     { '\'' }
| ['0'-'9']['0'-'9']['0'-'9']
   {
     let x = int (lexeme lexbuf)
     if x > 255 then
        error (info lexbuf) "Illegal character constant"
     else
        char x
   }
| [^ '"' '\\' 't' 'n' '\'']
    { error (info lexbuf) "Illegal character constant" }

(*  *)
